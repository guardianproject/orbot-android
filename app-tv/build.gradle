plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// Increments versionCode by ABI type
def abiCodeMap = ["armeabi-v7a": "1", "arm64-v8a": "2", "x86": "4", "x86_64": "5"]
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = abiCodeMap.get(output.getFilter("ABI"))
        if (baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(variant.versionCode) + Integer.valueOf(baseVersionCode)
        }
    }
}

android {
    namespace = "org.torproject.android.tv"
    compileSdk = 35

    defaultConfig {
        applicationId "org.torproject.android.tv"
        minSdk = 21
        targetSdk = 35
    }

    signingConfigs {
        release {
            if (!keystoreProperties.stringPropertyNames().isEmpty()) {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    flavorDimensions = ["teevee"]

    productFlavors {
        teevee {
            dimension "teevee"
            compileSdk 35
            versionCode 1741200300
            versionName "orbot-tv-1.0.1-tor-0.4.8.13"
            archivesBaseName = "Orbot-TV-$versionName"
        }
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

configurations {
    configureEach {
        exclude group: "androidx.lifecycle", module: "lifecycle-viewmodel-ktx"
    }
}

dependencies {
    implementation(project(":appcore"))
    implementation(project(":intentintegrator"))
    implementation(project(":orbotservice"))

    implementation(libs.android.material)
    implementation(libs.android.snowfall)
    implementation(libs.androidx.constraint)
    implementation(libs.androidx.coordinator)
    implementation(libs.androidx.leanback.core)
    implementation(libs.androidx.leanback.paging)
    implementation(libs.androidx.leanback.preferences)
    implementation(libs.androidx.leanback.tab)
    implementation(libs.androidx.palette)
    implementation(libs.androidx.recyclerview)
    implementation(libs.apl.appintro)
    implementation libs.androidx.core.ktx

    androidTestImplementation(libs.fastlane.screengrab)
}
